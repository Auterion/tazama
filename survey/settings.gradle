pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

def keystorePropertiesFile
for (file in getRootProject().getProjectDir().listFiles()) {
    if (file.name == "keystore.properties") {
        keystorePropertiesFile = file
    }
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        if (keystoreProperties.containsKey("MAPBOX_DOWNLOAD_TOKEN")) {
            maven {
                url 'https://api.mapbox.com/downloads/v2/releases/maven'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    // Do not change the username below.
                    // This should always be `mapbox` (not your username).
                    username = "mapbox"
                    // Use the secret token you stored in gradle.properties as the password
                    password = keystoreProperties["MAPBOX_DOWNLOAD_TOKEN"]
                }
            }
        }

        google()
        mavenCentral()
    }
}

rootProject.name = "tazama-survey"
include ':app'

includeBuild '../libvehicle'
includeBuild '../libviewmodel'
includeBuild '../libui'
//includeBuild '../ramani-maps/ramani-maplibre'
//includeBuild '../maplibre-plugins-android/plugin-annotation'

